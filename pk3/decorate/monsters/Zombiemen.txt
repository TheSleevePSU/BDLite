ACTOR BdZombieMan : BdLiteMonster Replaces ZombieMan
{
    Game Doom
    Health 40
    Radius 16
    Height 56
    Speed 6
    FastSpeed 12
    Mass 100
    PainChance 200
    PainChance "Kick", 255
    SeeSound "grunt/sight"
    AttackSound "weapons/rifle/fire"
    PainSound "grunt/pain"
    DeathSound "grunt/death"
    ActiveSound "grunt/active"
    DropItem "Clip"
    Obituary "%o was shot by a Soldier."
    damagefactor "Flak", 20.0
    Species Zombieman

    States
    {
    Spawn:
        PSSS A random(10,35) A_Look
        TNT1 A 0 A_Jump(128, "SkipLook1")
        PSSS C random(10,35) A_Look
    SkipLook1:
        PSSS B random(10,35) A_Look
        TNT1 A 0 A_Jump(128, "SkipLook2")
        PSSS D random(10,35) A_Look
    SkipLook2:
        Goto Spawn
    See:
        TNT1 A 0 A_JumpIfCloser(200, "FallBack")
        POSS AA 2 A_Chase
        POSS BB 2 A_Chase
        POSS CC 2 A_Chase
        POSS DD 2 A_Chase
        Loop

    Missile:
        TNT1 A 0 A_Stop
        TNT1 A 0 A_CheckSight("See") //If we don't have a line of sight (if we've come here from retreating, etc), give up
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        POSS E 6 A_FaceTarget
        POSS E 6 A_FaceTarget
        TNT1 A 0 A_CustomMissile("OrangeLensFlareAlt", 36, 20, 15, 0)
        TNT1 A 0 A_PlaySound("weapons/rifle/fire")
        POSS F 2 BRIGHT A_CustomMissile("ZombieManTracer", 32, 0, random(-8, 8), 1, random(-3, 3))
        TNT1 A 0 A_SpawnItemEx("BulletCasing", 17, 3, 35, 0, random(3,6), random(3,7))
        POSS E 2 A_FaceTarget
        TNT1 A 0 A_CustomMissile("OrangeLensFlareAlt", 36, 20, 15, 0)
        TNT1 A 0 A_PlaySound("weapons/rifle/fire")
        POSS F 2 BRIGHT A_CustomMissile("ZombieManTracer", 32, 0, random(-8, 8), 1, random(-3, 3))
        TNT1 A 0 A_SpawnItemEx("BulletCasing", 17, 3, 35, 0, random(3,6), random(3,7))
        POSS E 2 A_FaceTarget
        TNT1 A 0 A_CustomMissile("OrangeLensFlareAlt", 36, 20, 15, 0)
        TNT1 A 0 A_PlaySound("weapons/rifle/fire")
        POSS F 2 BRIGHT A_CustomMissile("ZombieManTracer", 32, 0, random(-8, 8), 1, random(-3, 3))
        TNT1 A 0 A_SpawnItemEx("BulletCasing", 17, 3, 35, 0, random(3,6), random(3,7))
        POSS E 2 A_FaceTarget
        TNT1 A 0 A_jump(64, "Missile")
        POSS E 6 A_FaceTarget
        Goto See
        
    FallBack:
        POSS D 2 A_FaceTarget
        POSS D 2 A_Recoil(2)
        POSS C 2 A_FaceTarget
        POSS C 2 A_Recoil(2)
        TNT1 A 0 A_Jump(64, "Missile")
        POSS B 2 A_FaceTarget
        POSS B 2 A_Recoil(2)
        POSS A 2 A_FaceTarget
        POSS A 2 A_Recoil(2)
        POSS D 2 A_FaceTarget
        POSS D 2 A_Recoil(2)
        POSS C 2 A_FaceTarget
        POSS C 2 A_Recoil(2)
        TNT1 A 0 A_Jump(128, "Missile")
        POSS B 2 A_FaceTarget
        POSS B 2 A_Recoil(2)
        POSS A 2 A_FaceTarget
        POSS A 2 A_Recoil(2)
        POSS D 2 A_FaceTarget
        POSS D 2 A_Recoil(2)
        POSS C 2 A_FaceTarget
        POSS C 2 A_Recoil(2)
        POSS B 2 A_FaceTarget
        POSS B 2 A_Recoil(2)
        POSS A 2 A_FaceTarget
        POSS A 2 A_Recoil(2)
        TNT1 A 0 A_Jump(256, "Missile")
    
    Pain:
        POSS G 5
        POSS G 5 A_Pain
        TNT1 A 0 A_Jump(128, "Missile")
        Goto See

    Death:
    Death.Bullet:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_Jump(256, "DieNormal", "DieNormal2")
    
    Death.Kick:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_Jump(256, "DieRollBack")

    Death.SuperKick:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_Jump(256, "DieSplit", "DieGibs")

    Death.Shotgun:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_Jump(256, "DieBlastedBack", "DieRollBack", "DieArmOff", "DieRambo")
    
    Death.RapidFire:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_Jump(256, "DieArmOff", "DieInHalf", "DieHeadOff", "DieHoleThroughChest")

    Death.Explosive:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_JumpIf(health > -10, "DieNormal")
        TNT1 A 0 A_Jump(256, "DieGibs", "DieX")

    Death.Plasma:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_Jump(256, "DiePlasma")
        
    Death.Flak:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_Jump(256, "DieJustLegs", "DieSplit", "DieGibs", "DieX")

    XDeath:
        TNT1 A 0 A_XScream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 A_Jump(256, "DieX")
        
    DieNormal:
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerSmall", 35, 0, random (0, 360), 2, random (0, 160))
        POSS HIJKL 6 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        POSS L -1
    DieNormal2:
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerSmall", 35, 0, random (0, 360), 2, random (0, 160))
        POSS RMNOP 6 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        POSS P -1
    DieBlastedBack:
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
        TNT1 A 0 ThrustThingZ(0,30,0,1)
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 A_Recoil(random(7, 10))
        POS4 UUVWXY 5 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        POSS L -1
        Stop
    DieRollBack:
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
        TNT1 A 0 ThrustThingZ(0,10,0,1)
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 A_Recoil(random(5, 7))
        POSL ABCDEF 5 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        POSL F -1
    DieArmOff:
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
        TNT1 A 0 A_CustomMissile ("BdGibZombieArm", 48, 0, random (0, 360), 2, random (0, 160))
        POS3 ABCDE 6 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        POS3 E -1
    DieInHalf:
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAAAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
        TNT1 A 0 A_CustomMissile ("BdGibZombieTopHalf", 48, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAAA 0 A_CustomMissile ("BdGibGut", 35, 0, random (0, 360), 2, random(25, 45))
        PP2X BCDE 6 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        PP2X E -1
    DieHeadOff:
        TNT1 A 0 A_CustomMissile ("BdGibZombieHead", 48, 0, random (0, 360), 2, random (0, 160))
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
        ZMAD GABCDE 6 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        ZMAD E -1
    DieHoleThroughChest:
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
        TNT1 AAAA 0 A_CustomMissile ("BdGibGut", 35, 0, random (0, 360), 2, random(25, 45))
        TNT1 A 0 A_CustomMissile ("BdGibZombieRibcage", 42, 0, random (0, 360), 2, random (10, 160))
        ZXZ1 ABCDE 6 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        ZXZ1 E -1
    DieRambo:
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
        12DG ABCDEFGHI 4 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        12DG I -1
    DiePlasma:
        PBR1 A 2
        TNT1 AAAAAAAAAAAAAAAAAAAAAAA 0 {
            A_CustomMissile ("BdAshes", 32, 0, random (0, 360), 2, random (0, 180));
            A_CustomMissile ("BdAshesHeavy", 32, 0, random (0, 360), 2, random (0, 180));
        }
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMelting", 35, 0, random (0, 360), 2, random (0, 160))
        PBR1 BBBBBB 2 A_SpawnItemEx("BdPlasmaEvaporate", random(0 - radius/2, radius/2), random(0 - radius/2, radius/2), 48)
        PBR1 CDEFGH 6 A_CustomMissile ("Blood", 10, 0, random (0, 360), 2, random (0, 160))
        TNT1 A 0 A_SpawnItem ("BdBloodPool")
        PBR1 HHHHHHHHHHHHHHHHHHHHHH 8 {
            A_CustomMissile ("MiniSmoke", 10, 0, random (0, 360), 2, random (0, 160));
            A_SpawnItemEx("BdPlasmaEvaporate", random(0 - radius/2, radius/2), random(0 - radius/2, radius/2), 8);
        }
        PBR1 H -1
        Stop    
    DieJustLegs:
        TNT1 AA 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAAAAAA 0 A_CustomMissile ("BdGibGut", 35, 0, random (0, 360), 2, random(25, 45))
        POSX AAAABCDE 6 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        POSX E -1
    DieSplit:
        TNT1 AA 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAAAAAA 0 A_CustomMissile ("BdGibGut", 35, 0, random (0, 360), 2, random(25, 45))
        ZBLD AAAABCDE 6 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        ZBLD F -1    
    DieGibs:
        TNT1 A 0 A_CustomMissile ("BdGibZombieArm", 48, 0, random (0, 360), 2, random (0, 160))
        TNT1 A 0 A_CustomMissile ("BdGibZombieLeg", 48, 0, random (0, 360), 2, random (0, 160))
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAAAAAA 0 A_CustomMissile ("BdGibGut", 35, 0, random (0, 360), 2, random(25, 45))
        POSS STUVW 6
        POSS X -1
    DieX:
        TNT1 AA 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAAAAAAA 0 A_CustomMissile ("BdBloodLump", 42, 0, random (0, 360), 2, random (10, 160))
        TNT1 AAAAAAAA 0 A_CustomMissile ("BdBloodLumpBone", 42, 0, random (0, 360), 2, random (10, 160))
        TNT1 A 0 A_CustomMissile ("BdGibZombieRibcage", 42, 0, random (0, 360), 2, random (10, 160))
        TNT1 AA 0 A_CustomMissile ("BdGibZombieArm", 42, 0, random (0, 360), 2, random (10, 160))
        TNT1 AA 0 A_CustomMissile ("BdGibZombieLeg", 42, 0, random (0, 360), 2, random (10, 160))
        TNT1 A 0 A_SpawnItem ("BdBloodSpot")
        Stop
    Raise:
        POSS LKJIH 6
        Goto See
    }
}
