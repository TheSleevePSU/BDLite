ACTOR BdPlasmaGun : BdLiteWeapon Replaces PlasmaRifle
{
	Weapon.SelectionOrder 100
	Weapon.AmmoGive2 50
	Weapon.SlotNumber 6
	Weapon.AmmoType1 "PlasmaAmmoLoaded"
	Weapon.AmmoType2 "PlasmaAmmo"
    Inventory.PickupSound "PLSDRAW"
	Inventory.PickupMessage "$GOTPLASMA"
	States
	{
	Spawn:
		PLAS A -1
		Stop
	Ready:
        TNT1 A 0 A_PlaySound("PLSDRAW")
		PLS9 ABC 1
    ReadyLoop:
		PLSG A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWZOOM)
		Goto ReadyLoop
		
	NoNeedToReload:
		Goto ReadyLoop
	Deselect:
        PLS9 CBA 1
		TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1
		Wait

	Fire:
        TNT1 A 0 A_JumpIfInventory("PlasmaAmmoLoaded", 1, 1)
        Goto Reload

        TNT1 A 0 A_PlaySound("PLSM9", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile("PlasmaFlareSpawn", 0, 0, 0, 0)
		PLSF A 1 BRIGHT A_FireCustomMissile("PlasmaBall", 0, 1, 0, 0)
		TNT1 A 0 A_Takeinventory("PlasmaAmmoLoaded", 1)
		PLSA B 1 BRIGHT A_FireCustomMissile("PlayerQuakeSmall", 0, 0, 0, 0)
		PLSA C 1 BRIGHT
		PLSF B 1 BRIGHT
		TNT1 A 0 A_ReFire
        TNT1 A 0 A_PlaySound("PLSCOOL", 0)
        PLSG BCD 1
		PLSG EEEEEE 3 {
			A_FireCustomMissile("SmokeTrail",0,0,10,2);
			A_WeaponReady(WRF_NOPRIMARY | WRF_ALLOWZOOM);
		}
		PLSG DCB 1
        TNT1 A 0 A_PlaySound("BEPBEP")
		Goto ReadyLoop
    Reload:
		TNT1 A 0 A_ClearReFire
		TNT1 A 0 A_JumpIfInventory("PlasmaAmmoLoaded", ACS_NamedExecuteWithResult("checkCapacity", 4), "ReadyLoop")
		TNT1 A 0 A_JumpIfInventory("PlasmaAmmo", 1, "ReloadAnimate")
		//No more ammo to reload
		TNT1 A 0 A_PlaySound("BEP")
		PLSR A 4 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 A_PlaySound("BEP")
		PLSR A 4 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 A_CheckReload
        Goto ReadyLoop
	ReloadAnimate:
        TNT1 A 0 A_PlaySound("PLREADY")
        PLSR AB 1
		TNT1 A 0 A_SpawnItemEx("EmptyCell", 17, 2, 35, 0, random(3,6), 4)
		PLSR CDEEEEEEEEEEEEEEEF 1
        PLSR GHHIIIJJJJJJJJIKLBAAAAA 1
	ReloadAmmo:
		TNT1 A 0 A_JumpIfInventory("PlasmaAmmoLoaded",ACS_NamedExecuteWithResult("checkCapacity", 4), "NoNeedToReload")
		TNT1 A 0 A_JumpIfInventory("PlasmaAmmo",1,1)
		Goto ReadyLoop
		TNT1 A 0 A_Giveinventory("PlasmaAmmoLoaded",1)
		TNT1 A 0 A_Takeinventory("PlasmaAmmo",1)
		Goto ReloadAmmo

	NoNeedToReload:
		Goto ReadyLoop
	}
}



ACTOR Plasma_Ball: FastProjectile Replaces PlasmaBall
{
	Radius 10
	Height 2
	Speed 60
	Damage 8
    DamageType Plasma
	Decal "SmallerScorch"
	Projectile
	+RANDOMIZE
	-NOBLOCKMAP
	+NOBLOOD
	+NORADIUSDMG
	damagefactor "Blood", 0.0 damagefactor "BlueBlood", 0.0 damagefactor "GreenBlood", 0.0 damagefactor "Taunt", 0.0 damagefactor "KillMe", 0.0 damagefactor "Shrapnel", 0.0
	Health 5
	Scale 1.0
	renderstyle ADD
	alpha 0.90
	Scale 0.10
	DeathSound "weapons/plasmax"
    SeeSound "None"
	Obituary "$OB_MPPLASMARIFLE"
	States
	{
	DM:
		TNT1 A 0
		TNT1 A 0 A_ChangeFLag("THRUSPECIES", 0)
		TNT1 A 0 A_ChangeFLag("MTHRUSPECIES", 0)
		Goto Spawn1
	
	Spawn:
		TNT1 A 0

	Spawn1:
        PBAL C 1 BRIGHT A_SpawnItem("PlasmaFlare",0,0)
		Loop

	Xdeath:
		TNT1 A 0 A_CustomMissile ("BluePlasmaFire", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 AAAAA 0 A_CustomMissile ("BluePlasmaParticle", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 B 1 A_Explode(6,50,0)
		TNT1 B 4
		TNT1 AAA 9 A_CustomMissile ("MiniSmoke", 1, 0, random (0, 360), 2, random (0, 160))
		Stop

	Death:
		TNT1 B 1 A_Explode(6,50,1)
		TNT1 A 0 A_SpawnItemEx ("DetectFloorCraterSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEx ("DetectCeilCraterSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_CustomMissile ("BluePlasmaFire", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 AAAAA 0 A_CustomMissile ("BluePlasmaParticle", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 B 4	
		TNT1 AAAAAAAAA 9 A_CustomMissile ("MiniSmoke", 1, 0, random (0, 360), 2, random (0, 160))
		Stop
	}
}

Actor PlasmaAmmo : Cell replaces Cell 
{
	Inventory.PickupMessage "$GOTCELL"
	Inventory.Amount 20
	Inventory.MaxAmount 300
	Ammo.BackpackAmount 20
	Ammo.BackpackMaxAmount 600
	Inventory.Icon "CELLA0"
	States
	{
	Spawn:
		CELL ABCDE 4
		Loop
	}
}

Actor PlasmaAmmoPack : PlasmaAmmo replaces CellPack
{
	Inventory.PickupMessage "$GOTCELLBOX"
	Inventory.Amount 100
	States
	{
	Spawn:
		CELP ABCDE 4
		Loop
	}
}

Actor PlasmaAmmoSingle : PlasmaAmmo
{
	Inventory.PickupMessage "Picked up a single cell"
	Inventory.Amount 1
	States
	{
	Spawn:
		CEL1 AB 6
		Loop
	}
}

ACTOR PlasmaAmmoLoaded : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 50
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 50
   Inventory.Icon "PLASA0"
}
