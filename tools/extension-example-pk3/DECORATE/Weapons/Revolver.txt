ACTOR BdRevolver : BDLiteWeapon
{
	Weapon.AmmoGive2 6
	Weapon.SlotNumber 2
	Weapon.SelectionOrder 2000
	Weapon.AmmoType1 "RevolverAmmoLoaded"
	Weapon.AmmoType2 "RifleAmmo"
	Weapon.SlotPriority 1
	Obituary "%o was revolved by %k's revolver."
    AttackSound "None"
    Inventory.PickupSound "weapons/rifle/clip"
	Inventory.Pickupmessage "Now you'll see why they call me Revolver!"
	States
	{
	Spawn:
		RVIC A -1
	Ready:
        TNT1 A 0 A_PlaySound("weapons/rifle/clip")
        REVR A 2 Offset(-50, 80)
		REVR A 2 Offset(-30, 65)
		REVR A 2 Offset(-15, 50)
	ReadyLoop:
		REVI A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWZOOM)
		Goto ReadyLoop
	Deselect:
		REVR A 2 Offset(-15, 50)
		REVR A 2 Offset(-30, 65)
        REVR A 2 Offset(-50, 80)
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1
		Wait
    Fire:
		TNT1 A 0 A_JumpIfInventory("RevolverAmmoLoaded", 1, 1)
        Goto Reload
        TNT1 A 0 A_PlaySound("weapons/rifle/fire")
		REVF A 3 BRIGHT A_AlertMonsters
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,30)
		REVF B 3 BRIGHT A_FireBullets (0, 0, 1, 14, "HitPuff")
		TNT1 A 0 A_Takeinventory("RevolverAmmoLoaded",1)
        TNT1 A 0 A_FireCustomMissile("Tracer", 0, 0, -1, -12, 0, random(-1,1))
        REVF C 3 A_SetPitch(pitch - 0.8)
		REVF D 3 A_SetPitch(pitch + 0.8)
		TNT1 A 0 A_Refire
		Goto ReadyLoop
    Reload:
		TNT1 A 0 A_JumpIfInventory("RevolverAmmoLoaded", 6, "ReadyLoop") //Check maximum capacity
        TNT1 A 0 A_JumpIfInventory("RifleAmmo",1,"ReloadAnimate")
		REVF C 4 A_PlaySound("weapons/empty")
		REVI A 4
		Goto ReadyLoop
	ReloadAnimate:
        REVR ABCDE 2 A_WeaponReady(WRF_NOPRIMARY | WRF_ALLOWZOOM)
		REVR F 2 A_PlaySound("weapons/sshoto")
		REVR G 2
        TNT1 AAAAAA 0 A_SpawnItemEx("BulletCasing", 17, 3, 35, 0, random(3,6), random(3,7))
		REVR HIJ 2
        TNT1 A 0 A_PlaySound("weapons/sshotc")
		REVR K 6
		REVR LM 2
		REVR DBA 2
	ReloadAmmo:
		TNT1 A 0 A_JumpIfInventory("RevolverAmmoLoaded", 6, "ReadyLoop")
		TNT1 A 0 A_JumpIfInventory("RifleAmmo", 1, 1)
		Goto ReadyLoop 
		TNT1 A 0 A_Giveinventory("RevolverAmmoLoaded",1)
		TNT1 A 0 A_Takeinventory("RifleAmmo",1)
		Goto ReloadAmmo
	}
}

ACTOR RevolverAmmoLoaded : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 6
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 6
   Inventory.Icon "RVICA0"
}
