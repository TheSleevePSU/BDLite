//===========================================================================
//
// Chaingunner
//
//===========================================================================
ACTOR BdCommando: BdLiteMonster Replaces ChaingunGuy
{
    Health 90
    Radius 16
    Height 56
    Mass 100
    Speed 5
    PainChance 200
    PainChance "Kick", 255
    Translation "192:207=176:191"
    damagefactor "Flak", 20.0
    MaxStepHeight 24
    MaxDropOffHeight 32
    +FLOORCLIP
    +MISSILEMORE
    +MISSILEEVENMORE
    SeeSound "grunt/sight"
    PainSound "grunt/pain"
    DeathSound "grunt/death"
    ActiveSound "grunt/active"
    DropItem "BdMinigun"
    Obituary "%o was chaingunned by a Commando."
    Species Commando
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_CheckSight("Spawn2")
        MPOS B 10 A_Look
        Loop
    Spawn2:
        MPOS B 1 A_Look
        Goto Spawn
        
    See:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        MPOS AA 2 A_Chase
        MPOS AA 2 A_Chase
        MPOS BB 2 A_Chase
        MPOS CC 2 A_Chase
        MPOS DD 2 A_Chase
        Loop

    Missile:
        TNT1 A 0 A_PlaySound("weapons/chaingun/start")
        MPOS E 12 A_FaceTarget
        MPOS E 12 A_FaceTarget

    MissileContinue:
        MPOS E 1 A_FaceTarget
        TNT1 AA 0 A_CustomMissile("OrangeLensFlareAltFast", 34, 10, 15, 0)
        MPOS F 1 BRIGHT A_CustomMissile("MonsterMinigunTracer", 38, 0, random(-9, 9), 1, random(-2, 2))
        TNT1 A 0 A_SpawnItemEx("BulletCasing", 17, 3, 35, 0, random(3,6), random(3,7))
        MPOS E 2 A_FaceTarget 
        TNT1 AA 0 A_CustomMissile("OrangeLensFlareAltFast", 34, 10, 15, 0)
        MPOS F 1 BRIGHT A_CustomMissile("MonsterMinigunTracer", 38, 0, random(-7, 7), 1, random(-2, 2))
        TNT1 A 0 A_SpawnItemEx("BulletCasing", 17, 3, 35, 0, random(3,6), random(3,7))
        MPOS E 2 A_FaceTarget 
        TNT1 AA 0 A_CustomMissile("OrangeLensFlareAltFast", 34, 10, 15, 0)
        MPOS F 1 BRIGHT A_CustomMissile("MonsterMinigunTracer", 38, 0, random(-9, 9), 1, random(-2, 2))
        TNT1 A 0 A_SpawnItemEx("BulletCasing", 17, 3, 35, 0, random(3,6), random(3,7))
        MPOS E 2 A_FaceTarget 
        TNT1 AA 0 A_CustomMissile("OrangeLensFlareAltFast", 34, 10, 15, 0)
        MPOS F 1 BRIGHT A_CustomMissile("MonsterMinigunTracer", 38, 0, random(-7, 7), 1, random(-2, 2))
        TNT1 A 0 A_SpawnItemEx("BulletCasing", 17, 3, 35, 0, random(3,6), random(3,7))
        MPOS E 2 A_FaceTarget 
        TNT1 AA 0 A_CustomMissile("OrangeLensFlareAltFast", 34, 10, 15, 0)
        MPOS F 1 BRIGHT A_CustomMissile("MonsterMinigunTracer", 38, 0, random(-9, 9), 1, random(-2, 2))
        TNT1 A 0 A_SpawnItemEx("BulletCasing", 17, 3, 35, 0, random(3,6), random(3,7))
        MPOS E 2 A_FaceTarget 
        TNT1 AA 0 A_CustomMissile("OrangeLensFlareAltFast", 34, 10, 15, 0)
        MPOS F 1 BRIGHT A_CustomMissile("MonsterMinigunTracer", 38, 0, random(-9, 9), 1, random(-2, 2))
        TNT1 A 0 A_SpawnItemEx("BulletCasing", 17, 3, 35, 0, random(3,6), random(3,7))
        TNT1 A 0 A_CheckSight("See")
        TNT1 A 0 A_CposRefire
        Goto MissileContinue

    Pain:
        MPOS G 5
        MPOS G 5 A_Pain
        TNT1 A 0 A_Jump(196, "Missile")
        Goto See
    
    Death:
    Death.Bullet:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_Jump(256, "DieNormal", "DieRambo")

    Death.Kick:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_Jump(256, "DieRollBack")

    Death.SuperKick:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_Jump(256, "DieJustLegs", "XDeath", "DieTornUp")

    Death.Shotgun:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_Jump(256, "DieRollBack", "DieArmOff", "DieRambo", "DieHoles")
    
    Death.RapidFire:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_Jump(256, "DieHoles", "DieTornUp", "DieArmOff", "DieHolesNoHead")

    Death.Explosive:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_JumpIf(health > -10, "DieNormal")
        TNT1 A 0 A_Jump(256, "XDeath")

    Death.Plasma:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_Jump(256, "DiePlasma")
        
    Death.Flak:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_Jump(256, "DieJustLegs", "DieTornUp", "XDeath")

    DieNormal:
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
        CPDR CEGIKMO 4 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        CPDR O -1
    DieRambo:
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
        CPDR A 4
        CPDR B 4 {
            A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40));
            A_PlaySound("weapons/chaingun/fire");
        }
        CPDR C 4
        CPDR D 4 {
            A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40));
            A_PlaySound("weapons/chaingun/fire");
        }
        CPDR E 4
        CPDR F 4 {
            A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40));
            A_PlaySound("weapons/chaingun/fire");
        }
        CPDR G 4
        CPDR H 4 {
            A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40));
            A_PlaySound("weapons/chaingun/fire");
        }
        CPDR I 4
        CPDR J 4 {
            A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40));
            A_PlaySound("weapons/chaingun/fire");
        }
        CPDR K 4
        CPDR L 4 {
            A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40));
            A_PlaySound("weapons/chaingun/fire");
        }
        CPDR M 4
        CPDR N 4 {
            A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40));
            A_PlaySound("weapons/chaingun/fire");
        }
        CPDR O -1
    DieRollBack:
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
        TNT1 A 0 ThrustThingZ(0,20,0,1)
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 A_Recoil(random(3, 4))
        CPHM ACDEF 5 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        CPHM F -1
    DieHoles:
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
        TNT1 AA 0 A_CustomMissile ("BdBloodLumpBone", 42, 0, random (0, 360), 2, random (10, 160))
        TNT1 AA 0 A_CustomMissile ("BdGibGut", 35, 0, random (0, 360), 2, random(25, 45))
        CPOD ABCDEFG 4 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        CPOD G -1
    DieHolesNoHead:
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
        TNT1 AAA 0 A_CustomMissile ("BdBloodLumpBone", 42, 0, random (0, 360), 2, random (10, 160))
        TNT1 AAA 0 A_CustomMissile ("BdGibGut", 35, 0, random (0, 360), 2, random(25, 45))
        TNT1 A 0 A_CustomMissile ("BdGibCommandoHead", 48, 0, random (0, 360), 2, random (0, 160))
        CPHS ABCDEF 4 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        CPHS F -1
    DieArmOff:
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
        TNT1 A 0 A_CustomMissile ("BdGibCommandoArm", 48, 0, random (0, 360), 2, random (0, 160))
        CPOD HIJKL 6 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        CPOD L -1
    DiePlasma:
        PBR1 A 2
        TNT1 AAAAAAAAAAAAAAAAAAAAAAA 0 {
            A_CustomMissile ("BdAshes", 32, 0, random (0, 360), 2, random (0, 180));
            A_CustomMissile ("BdAshesHeavy", 32, 0, random (0, 360), 2, random (0, 180));
        }
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerLarge", 35, 0, random (0, 360), 2, random (0, 160))
        PBR1 BBBBBB 2 {
            A_SpawnItemEx("BdPlasmaEvaporate", random(0 - radius/2, radius/2), random(0 - radius/2, radius/2), 48);
            A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40));
        }
        PBR1 CDEFGH 6 {
            A_CustomMissile ("Blood", 10, 0, random (0, 360), 2, random (0, 160));
            A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40));
        }
        TNT1 A 0 A_SpawnItem ("BdBloodPool")
        PBR1 HHHHHHHHHHHHHHHHHHHHHH 8 {
            A_CustomMissile ("MiniSmoke", 10, 0, random (0, 360), 2, random (0, 160));
            A_SpawnItemEx("BdPlasmaEvaporate", random(0 - radius/2, radius/2), random(0 - radius/2, radius/2), 8);
        }
        PBR1 H -1
        Stop
    DieJustLegs:
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
        TNT1 AAAA 0 A_CustomMissile ("BdBloodLumpBone", 42, 0, random (0, 360), 2, random (10, 160))
        TNT1 A 0 A_CustomMissile ("BdGibCommandoArm", 48, 0, random (0, 360), 2, random (0, 160))
        TNT1 A 0 A_CustomMissile ("BdGibCommandoLeg", 48, 0, random (0, 360), 2, random (0, 160))
        TNT1 A 0 A_CustomMissile ("BdGibCommandoHead", 48, 0, random (0, 360), 2, random (0, 160))
        MPSD ABCD 6 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        MPSD D -1
    DieTornUp:
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
        TNT1 AAAA 0 A_CustomMissile ("BdBloodLumpBone", 42, 0, random (0, 360), 2, random (10, 160))
        TNT1 A 0 A_CustomMissile ("BdGibCommandoArm", 48, 0, random (0, 360), 2, random (0, 160))
        TNT1 A 0 A_CustomMissile ("BdGibCommandoLeg", 48, 0, random (0, 360), 2, random (0, 160))
        TNT1 A 0 A_CustomMissile ("BdGibCommandoHead", 48, 0, random (0, 360), 2, random (0, 160))
        CPSC ABCDEFGH 6 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        CPSC H -1
    XDeath:
        TNT1 A 0 A_XScream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAAAAAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
        TNT1 AAAAAAAA 0 A_CustomMissile ("BdBloodLumpBone", 42, 0, random (0, 360), 2, random (10, 160))
        TNT1 A 0 A_CustomMissile ("BdGibZombieRibcage", 42, 0, random (0, 360), 2, random (10, 160))
        TNT1 AA 0 A_CustomMissile ("BdGibCommandoArm", 48, 0, random (0, 360), 2, random (0, 160))
        TNT1 AA 0 A_CustomMissile ("BdGibCommandoLeg", 48, 0, random (0, 360), 2, random (0, 160))
        TNT1 A 0 A_CustomMissile ("BdGibCommandoHead", 48, 0, random (0, 360), 2, random (0, 160))
        Stop
    Raise:
        CPDR OMKIGECA 6
        Goto See
    }
}
