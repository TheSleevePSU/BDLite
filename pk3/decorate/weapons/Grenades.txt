//Grenades aren't really a weapon, but they're added on to all weapons by default

/** Inventory class used to count up the time until a grenade explodes */
Actor GrenadeTime : Inventory
{
inventory.maxamount 6
}

/** Grenades aren't really implemented as weapons, but they can be used with the Zoom function on weapons. They're projectiles that bounce and are affected by gravity. */
ACTOR BdGrenade
{
    Radius 4
    Height 4
    Projectile
    Speed 30
    Damage 0
    Gravity 0.7
    Scale 0.2
    +MISSILE
    -NOGRAVITY
    -BLOODSPLATTER
    -EXTREMEDEATH
    +EXPLODEONWATER
    +SKYEXPLODE
    +DOOMBOUNCE
    BounceFactor 0.5
    WallBounceFactor 0.25
    Health 5
    SeeSound "weapons/grenade/bounce"
    DeathSound "none"
    Obituary "%o ate %k grenade."
    States
    {
    Spawn:
        GRND ABCDEFGH 2
        TNT1 A 0 A_GiveInventory("GrenadeTime", 1)
        TNT1 A 0 A_JumpIfInventory("GrenadeTime", 3, "Explode")
        Loop
    Death:
        TNT1 A 0
        GRND H 16
        TNT1 A 0 A_GiveInventory("GrenadeTime", 1)
        TNT1 A 0 A_JumpIfInventory("GrenadeTime", 3, "Explode")
        Loop
        
        
        
    Explode:
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_SpawnItem("FragGrenadeExplosion")
        Stop
    }
}

actor FragGrenadeExplosion
{
+NOBLOCKMAP
+MISSILE
Damagetype Explosive
DeathSound "explosions/explode"
States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Explode(200,400, 0)
        TNT1 A 0 A_Explode(200,200, 0)
        TNT1 A 0 A_SpawnItem ("BigRicochet", 0, -15)
        TNT1 A 0 A_SpawnItemEx ("DetectFloorCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 A 0 A_SpawnItemEx ("DetectCeilCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 A 0 A_SpawnItemEx ("ExplosionFlare",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 A 0 A_SpawnItemEx ("BarrelExplosionDamage",0,0,30,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 A 0 A_SpawnItemEx ("BarrelKaboom",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 AAAAAAA 0 A_CustomMissile ("ExplosionFlames", 0, 0, random (0, 360), 2, random (0, 360))
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("ExplosionParticleHeavy", 0, 0, random (0, 360), 2, random (0, 180))
        TNT1 AAAAAA 0 A_CustomMissile ("ExplosionParticleHeavy", 0, 0, random (0, 360), 2, random (0, 180))
        TNT1 A 0 Radius_Quake (2, 24, 0, 15, 0)
        TNT1 A 0 BRIGHT A_Scream
        TNT1 A 0 A_ALertMonsters
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("ShrapnelParticle", 0, 0, random (0, 360), 2, random (5, 90))
        TNT1 A 0 A_PlaySound("explosions/far", 3)
        TNT1 A 0 A_SpawnItem("SmokeColumn")
        TNT1 AAAAAAAAAAAAAAA 8 A_CustomMissile ("ExplosionSmoke", 1, 0, random (0, 360), 2, random (50, 130))
        Stop
    }
}

/** Small ammo for grenades */
ACTOR GrenadeAmmo : Ammo
{
    //$Category Ammunition
    Game Doom
    SpawnID 11
    Inventory.PickupMessage "You got a grenade!"
    Inventory.PickupSound "weapons/rifle/clip"
    Inventory.Amount 1
    Inventory.MaxAmount 6
    Ammo.BackpackAmount 5
    Ammo.BackpackMaxAmount 20
    Inventory.Icon "GRNDA"
    Scale 0.8
    States
    {
    Spawn:
        PGRN AB 10
        Loop
    }
}

/** Large ammo for grenades */
ACTOR GrenadeBox : GrenadeAmmo
{
    Game Doom
    SpawnID 139
    Inventory.PickupMessage "Picked up a box of grenades."
    Inventory.PickupSound "weapons/rifle/clip"
    Inventory.Amount 3
    States
    {
    Spawn:
        PGRN CD 10
        Loop
    }
}
        