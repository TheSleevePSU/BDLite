ACTOR BdRocketLauncher : BdLiteWeapon Replaces RocketLauncher
{
	SpawnID 29
	Weapon.SelectionOrder 9500
	Weapon.AmmoGive2 6
	Weapon.AmmoType1 "RocketAmmoLoaded"
	Weapon.AmmoType2 "RocketAmmo"
	Weapon.SlotNumber 5
    Inventory.PickupSound "weapons/rocket/pickup"
	Inventory.PickupMessage "$GOTLAUNCHER"

	States
	{
	Spawn:
		LAUN A -1
		Stop
	Ready:
        TNT1 A 8 A_PlaySound("weapons/rocket/draw")
        MISS ABCDEFG 1		
	ReadyLoop:
		MISG A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWZOOM)
        Goto ReadyLoop
	Deselect:
        MISS GFEDCBA 1
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1
		Wait
	Fire:
        TNT1 A 0 A_JumpIfInventory("RocketAmmoLoaded",1,1)
        Goto Reload

		TNT1 A 0 A_FireCustomMissile("RedFlareSpawn",-5,0,0,0)
        TNT1 A 0 A_PlaySound ("weapons/rocket/fire")
		MISF A 1 BRIGHT A_FireCustomMissile("BdRocket", 0, 1, 0, -10)
		TNT1 A 0 A_Takeinventory("RocketAmmoLoaded",1)
		MISF BCD 1 BRIGHT
        MISF E 1 A_SetPitch(-6.0 + pitch)
        MISG BCD 2 A_SetPitch(2.0 + pitch)
		MISG EF 2
        TNT1 A 0
        TNT1 A 0 A_Refire
        Goto ReadyLoop

    Reload:
		MISG A 1 A_WeaponReady
		MISG A 6
        TNT1 A 0 A_JumpIfInventory("RocketAmmoLoaded",ACS_NamedExecuteWithResult("checkCapacity", 3),"NoNeedToReload")
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",1, "ReloadAnimate")
        Goto ReadyLoop
	ReloadAnimate:
        TNT1 A 0 A_PlaySound("weapons/rocket/reload")
		TNT1 A 0 A_SetAngle(-2 + angle)
        TNT1 A 0 A_SetPitch(+2 + pitch)
        MISR ABCDDE 1 
        TNT1 A 0 A_SpawnItemEx("EmptyRocketChamber", 17, 2, 35, 0, random(3,6), 4)
        MISR EFFGG 1
		TNT1 A 0 A_SetAngle(+2 + angle)
        TNT1 A 0 A_SetPitch(-2 + pitch)
		MISR HHIIJJKLM 1
		TNT1 A 0 A_SetAngle(+1 + angle)
        MISR MMMNOP 1
		TNT1 A 0 A_SetAngle(-1 + angle)
		MISR Q 3
		MISR PR 1
		MISG DEF 2
		MISG A 6
	ReloadAmmo:
		//This continuously subtracts from stock and adds to the amount in the weapon until we either run out of stock or fill the weapon.
		TNT1 A 0 A_JumpIfInventory("RocketAmmoLoaded",ACS_NamedExecuteWithResult("checkCapacity", 3),"NoNeedToReload")
		TNT1 A 0 A_JumpIfInventory("RocketAmmo",1, 1)
		Goto ReadyLoop
		TNT1 A 0 A_Giveinventory("RocketAmmoLoaded", 1)
		TNT1 A 0 A_Takeinventory("RocketAmmo", 1)
		Goto ReloadAmmo

	NoNeedToReload:
		MISG A 1 A_Refire
		Goto ReadyLoop
	}
}

ACTOR RocketAmmoLoaded : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 6
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 6
   Inventory.Icon "LAUNA0"
}

ACTOR BdRocket Replaces Rocket
{
	Radius 10
	Height 8
	Projectile
	Speed 35
    Damage 35
	DamageType Explosive
    MeleeDamage 0
    Gravity 0.00
	Scale 0.7
    Decal "Scorch"
    -BLOODSPLATTER
    +EXTREMEDEATH
    +BLOODSPLATTER 
	+THRUSPECIES
	+MTHRUSPECIES
	SeeSound "weapons/rocket/fly"
	DeathSound "explosions/explode"
	Obituary "$OB_MPROCKET"
	Species "Marines"
	States
	{
	DM:
		TNT1 A 0
		TNT1 A 0 A_ChangeFLag("THRUSPECIES", 0)
		TNT1 A 0 A_ChangeFLag("MTHRUSPECIES", 0)
		Goto Spawn
	
	Spawn:
	    MISL A 1 Bright 
		TNT1 A 0 A_PlaySound("weapons/rocket/fly")
		TNT1 A 0 A_SpawnItem("RocketFlare",-20,0)
		TNT1 A 0 A_SpawnItem ("SmokeTrail")
		TNT1 A 0 A_CustomMissile ("SmokeTrail", 2, 0, random (160, 210), 2, random (-30, 30))
		Loop

	Death:
        TNT1 A 0 Radius_Quake (3, 8, 0, 15, 0)//(intensity, duration, damrad, tremrad, tid)
		TNT1 A 0 A_CustomMissile("BigRicochet")
        TNT1 A 0 A_SpawnItemEx ("DetectFloorCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
	    TNT1 A 0 A_SpawnItemEx ("DetectCeilCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 A 0 A_SpawnItemEx ("ExplosionFlare",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 AAAAAAAAA 0 A_CustomMissile ("ExplosionParticleHeavy", 0, 0, random (0, 360), 2, random (0, 180))
        TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("ExplosionParticleHeavy", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 AAAAAAAAA 0 A_CustomMissile ("ExplosionParticleVeryFast", 0, 0, random (0, 360), 2, random (0, 360))
        TNT1 AAAAA 0 A_CustomMissile ("MediumExplosionFlames", 0, 0, random (0, 360), 2, random (0, 360))
        TNT1 AAAAA 0 A_CustomMissile ("ExplosionSmoke", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 A 0 A_SpawnItemEx ("RocketExplosionDamage",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 2
		TNT1 A 0 A_PlaySound("explosions/far", 3)
		TNT1 A 20
		Stop
	}
}

actor RocketExplosionDamage
{
+NOBLOCKMAP
+MISSILE
+NOTELEPORT
Radius 2
Height 2
Damagetype Explosive
States
{
    Spawn:
		TNT1 A 0
		TNT1 A 0 A_Explode(50,150)
        Stop
	}
}