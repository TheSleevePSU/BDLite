ACTOR BdRevenant : BdLiteMonster Replaces Revenant
{
	GibHealth 45
	Health 300
	Radius 20
	Height 76
	MeleeThreshold 196
	Speed 10
	+FLOORCLIP
	PainChance "Kick", 255
    BloodType "RevenantBlood"
	SeeSound "skeleton/sight"
	PainSound "skeleton/pain"
	DeathSound "skeleton/death"
	ActiveSound "skeleton/active"
	MeleeSound "skeleton/melee"
	HitObituary "$OB_UNDEADHIT"
	Obituary "$OB_UNDEAD"
	MaxStepHeight 24
	MaxDropOffHeight 32
	+DONTHURTSPECIES
	Species Revenant
	States
	{
    Spawn:
		SKEL A 6 A_Look
		Loop
	See:
		SKEL AAA 2 A_Chase
		SKEL BBB 2 A_Chase
		SKEL CCC 2 A_Chase
		SKEL DDD 2 A_Chase
		Loop
	Melee:	    
		SKEL G 1 A_FaceTarget
		SKEL G 6 A_SkelWhoosh
		SKEL H 6 A_FaceTarget
		TNT1 A 0 A_CustomMissile("RevenantAttack",10,0,0,0)
		SKEL I 6
		Goto See
	Missile:	    
        TNT1 A 0 A_Jump (200, 4)
        TNT1 A 0
        Goto SpecialAttack
		SKEL J 1 BRIGHT A_FaceTarget 
		SKEL J 9 BRIGHT A_FaceTarget
		SKEL K 10 A_CustomMissile("RevenantBalls",50,15,0,0)
		Goto See
		
	Missile2:	
		SKEL K 10 A_FaceTarget
		SKEL J 1 BRIGHT A_FaceTarget 
		SKEL J 9 BRIGHT A_FaceTarget
		SKEL K 10 A_CustomMissile("RevenantBalls",50,-15,0,0)
		SKEL K 10 A_FaceTarget
		Goto See

	SpecialAttack:
		SKEL J 1 BRIGHT A_FaceTarget 
		SKEL J 11 BRIGHT A_FaceTarget
		SKEL K 8 A_CustomMissile("RevenantMissile",60,-30,0,0)
		SKEL J 8 A_FaceTarget
		SKEL K 8 A_CustomMissile("RevenantMissile",50,15,0,0)
		SKEL J 8 A_FaceTarget
		Goto See
	Pain:
		SKEL L 5
		SKEL L 5 A_Pain
		Goto See
	Pain.Kick:
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 A_Recoil(6)
		SKEL L 6 A_Pain  
		SKEL L 6
		Goto See
		
	Death:
	Death.Bullet:
		TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_Jump(256, "DieNormal")
	Death.Kick:
		TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_Jump(256, "DieNormal")
	Death.SuperKick:
		TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_Jump(256, "DieInHalf", "DieInHalf2", "XDeath")
	Death.Shotgun:
		TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_Jump(256, "DieInHalf", "DieInHalf2")
	Death.RapidFire:
		TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_Jump(256, "DieRip", "DieInHalf2")
	Death.Explosive:
		TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_JumpIf(health > -10, "DieNormal")
		TNT1 A 0 A_Jump(256, "XDeath")
	Death.Plasma:
		TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_Jump(256, "DiePlasma")
	Death.Flak:
		TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_Jump(256, "DieInHalf", "XDeath", "DieRip", "DieInHalf2")


	DieNormal:
		TNT1 AAA 0 A_CustomMissile ("BdBloodSpawnerSmall", 35, 0, random (0, 360), 2, random (0, 160))
		TNT1 AAAAAAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
		SKEL MNOPQ 7 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
		SKEL Q -1
	DieInHalf:
		TNT1 A 0 A_CustomMissile ("BdGibRevenantTopHalf", 35, 0, random (0, 360), 2, random (0, 160))
		TNT1 AAAAA 0 A_CustomMissile ("BdGibRevenantBone", 35, 0, random (0, 360), 2, random (0, 160))
		TNT1 AA 0 A_CustomMissile ("BdGibRevenantArm", 35, 0, random (0, 360), 2, random (0, 160))
		TNT1 AA 0 A_CustomMissile ("BdGibRevenantElbow", 35, 0, random (0, 360), 2, random (0, 160))
		TNT1 AAA 0 A_CustomMissile ("BdBloodSpawnerSmall", 35, 0, random (0, 360), 2, random (0, 160))
		TNT1 AAAAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
		TNT1 AA 0 A_CustomMissile ("BdGibGut", 35, 0, random (0, 360), 2, random(45, 80))
		SKEX ABCDE 8 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
		SKEX E -1
	DieInHalf2:
		TNT1 A 0 A_CustomMissile ("BdGibRevenantTopHalf", 35, 0, random (0, 360), 2, random (0, 160))
		TNT1 AAAAAA 0 A_CustomMissile ("BdGibRevenantBone", 35, 0, random (0, 360), 2, random (0, 160))
		TNT1 AA 0 A_CustomMissile ("BdGibRevenantArm", 35, 0, random (0, 360), 2, random (0, 160))
		TNT1 AA 0 A_CustomMissile ("BdGibRevenantElbow", 35, 0, random (0, 360), 2, random (0, 160))
		TNT1 AAA 0 A_CustomMissile ("BdBloodSpawnerSmall", 35, 0, random (0, 360), 2, random (0, 160))
		TNT1 AAAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
		TNT1 AAA 0 A_CustomMissile ("BdGibGut", 35, 0, random (0, 360), 2, random(45, 80))
		DKEL EFGHI 8 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
		DKEL I -1
	DieRip:
		TNT1 AAAAAAAAA 0 A_CustomMissile ("BdGibRevenantBone", 35, 0, random (0, 360), 2, random (0, 160))
		TNT1 AAA 0 A_CustomMissile ("BdBloodSpawnerSmall", 35, 0, random (0, 360), 2, random (0, 160))
		TNT1 AAAAAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
		TNT1 AAA 0 A_CustomMissile ("BdGibGut", 35, 0, random (0, 360), 2, random(45, 80))
		REVH EFGHIJKL 6 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
		REVH L -1
	DiePlasma:
		RVDP A 7
		TNT1 AAAAAAAAAAAAAAAAAAAAAAA 0 {
			A_CustomMissile ("BdAshes", 32, 0, random (0, 360), 2, random (0, 180));
			A_CustomMissile ("BdAshesHeavy", 32, 0, random (0, 360), 2, random (0, 180));
		}
		TNT1 AA 0 A_CustomMissile ("BdBloodSpawnerMelting", 35, 0, random (0, 360), 2, random (0, 160))
		TNT1 AAA 0 A_CustomMissile ("BdBloodSpawnerLarge", 35, 0, random (0, 360), 2, random (0, 160))
		RVDP BBBCCC 3 {
			A_SpawnItemEx("BdPlasmaEvaporate", random(0 - radius/2, radius/2), random(0 - radius/2, radius/2), 48);
			A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40));
		}
		RVDP DEFG 3 {
			A_CustomMissile ("Blood", 10, 0, random (0, 360), 2, random (0, 160));
			A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40));
		}
		TNT1 A 0 A_SpawnItem ("BdBloodPool")
		RVDP GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 4 {
			A_CustomMissile ("MiniSmoke", 10, 0, random (0, 360), 2, random (0, 160));
			A_SpawnItemEx("BdPlasmaEvaporate", random(0 - radius/2, radius/2), random(0 - radius/2, radius/2), 16);
		}
		RVDP G -1
	XDeath:
		TNT1 A 0 A_CustomMissile ("BdGibRevenantTopHalf", 35, 0, random (0, 360), 2, random (0, 160))
		TNT1 AAAAAAAAAAAAA 0 A_CustomMissile ("BdGibRevenantBone", random(15, 40), 0, random (0, 360), 2, random (0, 160))
		TNT1 AA 0 A_CustomMissile ("BdGibRevenantArm", 35, 0, random (0, 360), 2, random (0, 160))
		TNT1 AAAA 0 A_CustomMissile ("BdGibRevenantElbow", 35, 0, random (0, 360), 2, random (0, 160))
		TNT1 AA 0 A_CustomMissile ("BdBloodSpawnerSmall", 35, 0, random (0, 360), 2, random (0, 160))
		TNT1 AAAAAAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
		TNT1 AAAA 0 A_CustomMissile ("BdGibGut", 35, 0, random (0, 360), 2, random(45, 80))
		Stop
	Raise:
		REVH LKJIHGFE 6
		Goto See
	}
}

ACTOR RevenantBalls Replaces RevenantTracer
{
	Game Doom
	SpawnID 53
	Radius 10
	Height 4
	Speed 6
	FastSpeed 8
	Damage (random(15,20))
	PROJECTILE
	+THRUGHOST
	+SEEKERMISSILE
	Scale 1.0
    damagetype "bLAST"
	SeeSound "skeleton/attack"
	DeathSound "skeleton/tracex"
	States
	{
	Spawn:
	    TNT1 A 0 A_JumpIf(waterlevel > 1, "SpawnUnderwater")
		FATB AB 1 BRIGHT A_SpawnItem("RedFlareSmall",0,0)
        TNT1 A 0 A_SeekerMissile (4,8)
		TNT1 A 0 A_SpawnItemEx("RevenantProjectileTrails", 0, 0, 0, 0, 0, 0, 0, 128) 
		TNT1 A 0 A_SpawnItem ("SmokeTrail")
		TNT1 A 0 A_CustomMissile ("SmokeTrail", 2, 0, random (160, 210), 2, random (-30, 30))
		Loop
	Death:
	    TNT1 A 0 Radius_Quake (2, 54, 0, 15, 0)
		TNT1 AAA 0 A_CustomMissile ("ExplosionSmoke", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 A 0 A_SpawnItemEx ("DetectFloorCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEx ("DetectCeilCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 Bright A_Explode(7, 120, 1)
		TNT1 A 0 A_SpawnItemEx ("ExplosionFlare",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 A 0 A_SpawnItemEx("ExplosionParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
		TNT1 A 0
        TNT1 AAAAA 0 A_CustomMissile ("SmallExplosionFlames", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 A 0 A_ChangeFlag("SHOOTABLE", 0)
		TNT1 A 10
		Stop
	}
}

ACTOR RevenantMissile
{
	Game Doom
	SpawnID 53
	Radius 4
	Height 4
	Speed 8
	FastSpeed 12
	Damage (random(15,25))
	PROJECTILE
	+THRUGHOST
	+SEEKERMISSILE
	Scale 1.0
    damagetype "Blast"
	SeeSound "skeleton/attack"
	DeathSound "skeleton/tracex"
	States
	{
	Spawn:
		FATB AB 1 BRIGHT A_SpawnItem("YellowFlareSmall",0,0)
		TNT1 A 0 A_SpawnItemEx("RevenantProjectileTrails", 0, 0, 0, 0, 0, 0, 0, 128) 
		TNT1 A 0 A_SpawnItem ("SmokeTrail")
		TNT1 A 0 A_CustomMissile ("SmokeTrail", 2, 0, random (160, 210), 2, random (-30, 30))
		Loop
	Death:
	    TNT1 AAA 0 A_CustomMissile ("ExplosionSmoke", 0, 0, random (0, 360), 2, random (0, 360))
	    TNT1 A 0 Radius_Quake (2, 54, 0, 15, 0)
		TNT1 A 0 Bright A_Explode(7, 120, 1)
        TNT1 A 0 A_SpawnItemEx("ExplosionParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
		TNT1 A 0 A_SpawnItemEx ("DetectFloorCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
	TNT1 A 0 A_SpawnItemEx ("DetectCeilCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx ("ExplosionFlare",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 AAAA 0 A_CustomMissile ("SmallExplosionFlames", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 A 0 A_ChangeFlag("SHOOTABLE", 0)
		TNT1 A 10
		Stop
	}
}
	
ACTOR RevenantAttack: BaronBall
{
	Radius 24
	Height 16
	DamageType RVFT
	Projectile 
	+RANDOMIZE
	+FORCEXYBILLBOARD
    +THRUGHOST
	RenderStyle Add
	Alpha 0.6
    Damage 10
    Speed 28
	//HitObituary "$OB_IMPHIT"
	SeeSound "none"
	DeathSound "none"
	Decal "none"
	States
	{
	Spawn:
		TNT1 A 1 BRIGHT
        TNT1 A 1
		Goto Death
	Death:
		TNT1 A 0 BRIGHT
		Stop
	}
}
