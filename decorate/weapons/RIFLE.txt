ACTOR RifleAmmo : Ammo Replaces Clip
{
	Game Doom
	SpawnID 11
	Inventory.PickupMessage "Picked up a Magazine."
	Inventory.Amount 10
	Inventory.MaxAmount 300
	Ammo.BackpackAmount 10
	Ammo.BackpackMaxAmount 600
	Inventory.Icon "CLIPA0"
	States
	{
	Spawn:
		CLIP A -1
		Stop
	}
}


ACTOR RifleAmmoBox : RifleAmmo Replaces Clipbox
{
	Game Doom
	SpawnID 139
	Inventory.PickupMessage "Picked up a box of rifle ammunition."
	Inventory.Amount 100
	States
	{
	Spawn:
		AMMO A -1
		Stop
	}
}

ACTOR BdRifle : BDLiteWeapon Replaces Pistol
{
	Weapon.AmmoGive2 10
	Weapon.SlotNumber 2
	Weapon.SelectionOrder 4500
	Weapon.AmmoType1 "RifleAmmoLoaded"
	Weapon.AmmoType2 "RifleAmmo"
	Obituary "%o was shot down by %k's assault rifle."
    AttackSound "None"
    Inventory.PickupSound "CLIPIN"
	Inventory.Pickupmessage "You got the Assault Rifle!"
	States
	{
	Spawn:
		RIFL A -1
		Stop
	PickUp:
		Stop
	Ready:
        TNT1 A 0 A_PlaySound("CLIPIN")
        RIFS ABC 1
	ReadyLoop:
		RIFG A 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWZOOM)
		Goto ReadyLoop
	Deselect:
		RIFS CBA 1
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1
		Wait
    Fire:
		TNT1 A 0 A_JumpIfInventory("RifleAmmoLoaded", 1, 1)
        Goto Reload
        TNT1 A 0 A_PlaySound("weapons/pistol")
		RIFF A 1 BRIGHT A_AlertMonsters
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,30)
		RIFF B 1 BRIGHT A_FireBullets (0, 0, 1, 9, "HitPuff")
        TNT1 A 0 A_FireCustomMissile("Tracer", 0, 0, -1, -12, 0, random(-1,1))
        TNT1 A 0 A_SetPitch(-1.3 + pitch)
		TNT1 A 0 A_Takeinventory("RifleAmmoLoaded",1)
		TNT1 A 0 A_SetPitch(+0.6 + pitch)
		RIFF C 1 A_SpawnItemEx("BulletCasing", 17, 3, 35, 0, random(3,6), random(3,7))
        RIFG A 1 A_SetPitch(+0.5 + pitch)
		TNT1 A 0 A_Refire
		Goto ReadyLoop
    Reload:
		TNT1 A 0 A_JumpIfInventory("RifleAmmoLoaded",ACS_NamedExecuteWithResult("checkCapacity", 1), "ReadyLoop")
        TNT1 A 0 A_JumpIfInventory("RifleAmmo",1,"ReloadAnimate")
		RIFG A 8 A_PlaySound("weapons/empty")
		Goto ReadyLoop

	ReloadAnimate:
        TNT1 A 0 A_PlaySound("Reload")
        RIFR ABCDE 1 A_WeaponReady(WRF_ALLOWZOOM | WRF_NOFIRE)
        RIFR F 1 A_SpawnItemEx("EmptyClip", 17, 2, 35, 0, random(3,6), 4)
        RIFR G 8
		RIFR HIKL 1
		RIFR M 3
		RIFR NOPQRST 1 A_WeaponReady(WRF_ALLOWZOOM | WRF_NOFIRE)

	ReloadAmmo:
		TNT1 AAAA 0
		TNT1 A 0 A_JumpIfInventory("RifleAmmoLoaded",ACS_NamedExecuteWithResult("checkCapacity", 1), "ReadyLoop")
		TNT1 A 0 A_JumpIfInventory("RifleAmmo", 1, 1)
		Goto ReadyLoop 
		TNT1 A 0 A_Giveinventory("RifleAmmoLoaded",1)
		TNT1 A 0 A_Takeinventory("RifleAmmo",1)
		Goto ReloadAmmo
	}
}

ACTOR RifleAmmoLoaded : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 31
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 31
   Inventory.Icon "RIFLA0"
}
