ACTOR BdShotgunGuy: BdLiteMonster Replaces ShotgunGuy
{
    Health 50
    Radius 16
    Height 56
    Mass 100
    Speed 6
    FastSpeed 12
    PainChance 200
    DropItem "Shotgun"
    SeeSound "grunt/sight"
    PainSound "grunt/pain"
    DeathSound "grunt/death"
    ActiveSound "grunt/active"
    Obituary "%o got his face splattered on the wall by a Zombie Shotgunner."
    damagefactor "Flak", 20.0
    MaxStepHeight 24
    MaxDropOffHeight 32
	Species Sergeant
    States
    {
    Spawn:
          SPOS B 1
          Goto Stand
    Stand:
        TNT1 A 0 A_CheckSight("Stand2")
        SHID A 10 A_Look
        SHID A 10 A_Look
        SHID B 10 A_Look
        SHID B 10 A_Look
        Loop
    Stand2:
        SPSS Z 1 A_Look
        Goto Stand

    FallBack:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 1)
        SPOS D 2 A_FaceTarget
        SPOS D 2 A_Recoil(2)
        TNT1 A 0 A_jump(64, "Missile")
        SPOS C 2 A_FaceTarget
        SPOS C 2 A_Recoil(2)
        SPOS B 2 A_FaceTarget
        SPOS B 2 A_Recoil(2)
        SPOS A 2 A_FaceTarget
        SPOS A 2 A_Recoil(2)
        SPOS D 2 A_FaceTarget
        SPOS D 2 A_Recoil(2)
        TNT1 A 0 A_jump(64, "Missile")
        SPOS C 2 A_FaceTarget
        SPOS C 2 A_Recoil(2)
        SPOS B 2 A_FaceTarget
        SPOS B 2 A_Recoil(2)
        SPOS A 2 A_FaceTarget
        SPOS A 2 A_Recoil(2)
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        Goto Missile
        
    See:
        TNT1 A 0 A_JumpIfCloser(200, "FallBack")
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        SPOS AA 2 A_Chase
        SPOS BB 2 A_Chase
        SPOS CC 2 A_Chase
        SPOS DD 2 A_Chase
        Loop
        
    Missile:
        TNT1 A 0 A_Stop
        TNT1 A 0 A_CheckSight("See") //If we don't have a line of sight (if we've come here from retreating, etc), give up
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        SPOS D 5 A_FaceTarget
        SPOS E 14 A_FaceTarget
        TNT1 AA 0 A_CustomMissile("OrangeLensFlareAlt", 34, 10, 15, 0)
        TNT1 AAAAA 0 A_CustomMissile("MonsterTracer", 36, 0, random(-7, 7), 1, random(-4, 4))
        TNT1 AAAAAAAA 0 A_CustomMissile("ShotgunParticles", 36, 0, random(-9, 9), 6, random(-9, 9))
        TNT1 A 0 A_PlaySound("weapons/shotgun/fire")
        SPOS F 5 BRIGHT
        TNT1 A 0 A_Recoil(1)
        SPSR C 6
        SPOS E 7 A_FaceTarget
        SPSR A 7
        TNT1 A 0 A_PlaySound("weapons/shotgun/pump")
        SPSR B 7 A_SpawnItemEx("ShotgunCasing", 17, 3, 35, 0, random(3,6), random(3,7))
        SPSR A 7
        Goto See

    Pain:
        SPOS G 5
        SPOS G 5 A_Pain
        TNT1 A 0 A_Jump(196, "Missile")
        Goto See
        
    Death:
    Death.Bullet:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_Jump(256, "DieNormal", "DieNormal2")
    
    Death.Kick:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_Jump(256, "DieRollBack")

    Death.SuperKick:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_Jump(256, "DieSplit", "DieGibs")

    Death.Shotgun:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_Jump(256, "DieBlastedBack", "DieRollBack", "DieArmOff", "DieRoll")
    
    Death.RapidFire:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_Jump(256, "DieArmOff", "DieInHalf", "DieHeadOff", "DieHoleThroughChest", "DieRoll")

    Death.Explosive:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_JumpIf(health > -10, "DieNormal")
        TNT1 A 0 A_Jump(256, "DieGibs", "XDeath")

    Death.Plasma:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_Jump(256, "DiePlasma")
        
    Death.Flak:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_Jump(256, "DieJustLegs", "DieInHalf", "DieGibs")
        
    DieNormal:
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerSmall", 35, 0, random (0, 360), 2, random (0, 160))
        SPOS HIJKL 6 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        SPOS L -1
    DieNormal2:
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerSmall", 35, 0, random (0, 360), 2, random (0, 160))
        13DG JKLMN 6 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        13DG N -1
    DieBlastedBack:
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
        TNT1 A 0 ThrustThingZ(0,30,0,1)
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 A_Recoil(random(7, 10))
        SPO4 UUVWXY 5 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        SPOS L -1
        Stop
    DieRollBack:
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
        TNT1 A 0 ThrustThingZ(0,10,0,1)
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 A_Recoil(random(5, 7))
        SOSL GHIJKL 5 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        SOSL L -1
    DieArmOff:
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
        TNT1 A 0 A_CustomMissile ("BdGibShotgunGuyArm", 48, 0, random (0, 360), 2, random (0, 160))
        SPO5 ABDE 6 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        SPO5 E -1
    DieInHalf:
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAAAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
        TNT1 A 0 A_CustomMissile ("BdGibShotgunGuyTopHalf", 48, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAAA 0 A_CustomMissile ("BdGibGut", 35, 0, random (0, 360), 2, random(25, 45))
        SP2X BCDE 6 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        SP2X E -1
    DieHeadOff:
        TNT1 A 0 A_CustomMissile ("BdGibZombieHead", 48, 0, random (0, 360), 2, random (0, 160))
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
        SPDH ABCDE 6 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        SPDH E -1
    DieHoleThroughChest:
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
        TNT1 AAAA 0 A_CustomMissile ("BdGibGut", 35, 0, random (0, 360), 2, random(25, 45))
        TNT1 A 0 A_CustomMissile ("BdGibZombieRibcage", 42, 0, random (0, 360), 2, random (10, 160))
        ZXZ7 ABCDE 6 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        ZXZ7 E -1
    DieRoll:
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAA 0 A_CustomMissile ("BdBloodLump", 35, 0, random (0, 360), 2, random(45, 80))
        TNT1 A 0 ThrustThingZ(0,5,0,1)
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 A_Recoil(random(2, 4))
        SPO4 ABCDJ 4 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        SPO4 J -1
    DiePlasma:
        PBR1 A 2
        TNT1 AAAAAAAAAAAAAAAAAAAAAAA 0 {
            A_CustomMissile ("BdAshes", 32, 0, random (0, 360), 2, random (0, 180));
            A_CustomMissile ("BdAshesHeavy", 32, 0, random (0, 360), 2, random (0, 180));
        }
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMelting", 35, 0, random (0, 360), 2, random (0, 160))
        PBR1 BBBBBB 2 A_SpawnItemEx("BdPlasmaEvaporate", random(0 - radius/2, radius/2), random(0 - radius/2, radius/2), 48)
        PBR1 CDEFGH 6 A_CustomMissile ("Blood", 10, 0, random (0, 360), 2, random (0, 160))
        TNT1 A 0 A_SpawnItem ("BdBloodPool")
        PBR1 HHHHHHHHHHHHHHHHHHHHHH 8 {
            A_CustomMissile ("MiniSmoke", 10, 0, random (0, 360), 2, random (0, 160));
            A_SpawnItemEx("BdPlasmaEvaporate", random(0 - radius/2, radius/2), random(0 - radius/2, radius/2), 8);
        }
        PBR1 H -1
        Stop    
    DieJustLegs:
        TNT1 AA 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAAAAAA 0 A_CustomMissile ("BdGibGut", 35, 0, random (0, 360), 2, random(25, 45))
        SZD1 CCCCDEFGH 6 A_CustomMissile ("BdBloodDrop", 15, 0, random (0, 360), 2, random (0, 40))
        SZD1 H -1
    DieGibs:
        TNT1 A 0 A_CustomMissile ("BdGibShotgunGuyArm", 48, 0, random (0, 360), 2, random (0, 160))
        TNT1 A 0 A_CustomMissile ("BdGibShotgunGuyLeg", 48, 0, random (0, 360), 2, random (0, 160))
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAAAAAA 0 A_CustomMissile ("BdGibGut", 35, 0, random (0, 360), 2, random(25, 45))
        POSS STUVW 6
        POSS X -1
    XDeath:
        TNT1 A 0 A_XScream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_FaceTarget
        TNT1 AA 0 A_CustomMissile ("BdBloodSpawnerMed", 35, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAAAAAAA 0 A_CustomMissile ("BdBloodLump", 42, 0, random (0, 360), 2, random (10, 160))
        TNT1 AAAAAAAA 0 A_CustomMissile ("BdBloodLumpBone", 42, 0, random (0, 360), 2, random (10, 160))
        TNT1 A 0 A_CustomMissile ("BdGibZombieRibcage", 42, 0, random (0, 360), 2, random (10, 160))
        TNT1 AA 0 A_CustomMissile ("BdGibShotgunGuyArm", 42, 0, random (0, 360), 2, random (10, 160))
        TNT1 AA 0 A_CustomMissile ("BdGibShotgunGuyLeg", 42, 0, random (0, 360), 2, random (10, 160))
        TNT1 AAAAAAA 0 A_CustomMissile ("BdGibGut", 35, 0, random (0, 360), 2, random(25, 45))
        TNT1 A 0 A_SpawnItem ("BdBloodSpot")
        TNT1 A 1 //1-tic delay required after XDeath
        Stop
	Raise:
		SPOS LKJIH 6
		Goto See
    }
}
