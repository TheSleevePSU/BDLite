// Invulnerability Sphere ---------------------------------------------------

ACTOR BdInvulnerabilitySphere  : PowerupGiver Replaces InvulnerabilitySphere
{
    Game Doom
    SpawnID 133
    +COUNTITEM
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.ALWAYSPICKUP
    +INVENTORY.BIGPOWERUP
    Inventory.MaxAmount 0
    Powerup.Type Invulnerable
    Powerup.Color White //InverseMap
    Inventory.PickupMessage "$GOTINVUL"
    States
    {
    Spawn:
        PINV AAABBBCCCDDD 2 Bright A_SpawnItem("GreenFlareMedium",0,20)
        Loop
    }
}

// Soulsphere --------------------------------------------------------------

ACTOR BdSoulSphere : CustomInventory Replaces SoulSphere
{
    +COUNTITEM
    +INVENTORY.ALWAYSPICKUP
    Inventory.PickupMessage "Soulsphere"
    Inventory.PickupSound "misc/p_pkup"
    States
    {
    Spawn:
        SOUL AAABBBCCCDDDCCCBBB 2 Bright A_SpawnItem("BlueFlareMedium2",0,20)
        Loop
    Pickup:
        TNT1 A 0 A_GiveInventory("SoulSphereHealth", 1)
        Stop
    }
}

ACTOR SoulSphereHealth : Health    
{
    Inventory.Amount 100
    Inventory.MaxAmount 200
    +INVENTORY.ALWAYSPICKUP
}

ACTOR BdMegaSphere : CustomInventory Replaces Megasphere
{
    +COUNTITEM
    +INVENTORY.ALWAYSPICKUP
    Inventory.PickupMessage "Megasphere"
    Inventory.PickupSound "misc/p_pkup"
    States
    {
    Spawn:
        MEGA AABBCCDD 2 BRIGHT A_SpawnItem("LensFlare",0,35)
        Loop
    Pickup:
        TNT1 A 0 A_GiveInventory("BlueArmorForMegasphere", 1)
        TNT1 AA 0 A_GiveInventory("SoulsphereHealth", 1)
        Stop
    }
}    


// infrared -----------------------------------------------------------------

ACTOR BdNightVision : CustomInventory Replaces InfraRed
{
    Game Doom
    SpawnID 138
    +COUNTITEM
    +INVENTORY.ALWAYSPICKUP
    Inventory.PickupMessage "You got the Infrared Vision Googles."
    States
    {
    Spawn:
        PVIS E 7
        PVIS D 3 Bright A_SpawnItem("RedFlareVerySmall",0,10)
        PVIS C 2 Bright A_SpawnItem("RedFlareVerySmall",0,10)
        PVIS BAB 1 Bright A_SpawnItem("RedFlareVerySmall",0,10)
        PVIS C 2 Bright A_SpawnItem("RedFlareVerySmall",0,10)
        PVIS D 3 Bright A_SpawnItem("RedFlareVerySmall",0,10)
        Loop
    Pickup:
        TNT1 A 1
        TNT1 A 0 A_GiveInventory("NightVisionGiver", 1)
        Stop
        
    }
}


ACTOR NightVisionGiver : PowerupGiver
{
    Game Doom
    SpawnID 138
    +COUNTITEM
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.ALWAYSPICKUP
    Inventory.MaxAmount 0
    Powerup.Type LightAmp
    Inventory.PickupMessage "$GOTVISOR"
    Powerup.Colormap 0.0, 0.0, 0.0, 0.2,1.0,0.2
    Powerup.Duration 1050
    States
    {
    Spawn:
        PVIS E 7
        PVIS D 3 Bright A_SpawnItem("RedFlareVerySmall",0,10)
        PVIS C 2 Bright A_SpawnItem("RedFlareVerySmall",0,10)
        PVIS BAB 1 Bright A_SpawnItem("RedFlareVerySmall",0,10)
        PVIS C 2 Bright A_SpawnItem("RedFlareVerySmall",0,10)
        PVIS D 3 Bright A_SpawnItem("RedFlareVerySmall",0,10)
        Loop
        
    }
}

// Berserk ------------------------------------------------------------------

ACTOR BdBerserk : CustomInventory Replaces Berserk
{
    Game Doom
    Scale 1.2
    SpawnID 134
    +COUNTITEM
    +INVENTORY.ALWAYSPICKUP
    Inventory.PickupMessage "$GOTBERSERK"
    Inventory.PickupSound "misc/p_pkup"
    Inventory.PickupSound "pickups/berserk"
    States
    {
    Spawn:
        PSTR ABCB 4 A_SpawnItem("RedFlareVerySmall",0,10)
        Loop
    Pickup:
        TNT1 A 0 A_GiveInventory("PowerStrength")
        TNT1 A 0 HealThing(100, 0)
        TNT1 A 0 A_SelectWeapon("BdMeleeAttacks")
        TNT1 A 20
        Stop
    }
}

// Backpack ---------------------------------------------------------------

ACTOR BdBackpack : BackpackItem Replaces Backpack
{
    Game Doom
    SpawnID 144
    Height 26
    Inventory.PickupMessage "$GOTBACKPACK"
    States
    {
    Spawn:
        BPAK A -1
        Stop
    Pickup:
        TNT1 A 0 A_GiveInventory("ShellBox", 1)
        TNT1 A 0 A_GiveInventory("RifleAmmoBox", 1)
        TNT1 A 0 A_GiveInventory("PlasmaAmmoPack", 1)
        TNT1 A 0 A_GiveInventory("RocketBox", 1)
        Stop
    }
}