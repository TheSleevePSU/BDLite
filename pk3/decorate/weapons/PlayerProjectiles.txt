ACTOR Plasma_Ball: FastProjectile Replaces PlasmaBall
{
    Radius 10
    Height 2
    Speed 60
    Damage 8
    DamageType Plasma
    Decal "SmallerScorch"
    Projectile
    -RANDOMIZE
    -NOBLOCKMAP
    +NOBLOOD
    +NORADIUSDMG
    Health 5
    renderstyle ADD
    alpha 0.90
    Scale 0.10
    DeathSound "weapons/plasmax"
    SeeSound "None"
    Obituary "$OB_MPPLASMARIFLE"
    States
    {
    DMSpawn:
        TNT1 A 0
        TNT1 A 0 A_ChangeFLag("THRUSPECIES", 0)
        TNT1 A 0 A_ChangeFLag("MTHRUSPECIES", 0)
        Goto Spawn1
    
    Spawn:
        TNT1 A 1
        TNT1 A 0 ACS_NamedExecute("adjustForDeathmatch", 0, 0, 0, 0)
    Spawn1:
        PBAL C 1 BRIGHT A_SpawnItem("PlasmaFlare",0,0)
        Loop

    Xdeath:
        TNT1 A 0 A_CustomMissile ("BluePlasmaFire", 0, 0, random (0, 360), 2, random (0, 360))
        TNT1 AAAAA 0 A_CustomMissile ("BluePlasmaParticle", 0, 0, random (0, 360), 2, random (0, 360))
        TNT1 B 1 A_Explode(6,50,0)
        TNT1 B 4
        TNT1 AAA 9 A_CustomMissile ("MiniSmoke", 1, 0, random (0, 360), 2, random (0, 160))
        Stop

    Death:
        TNT1 B 1 A_Explode(6,50,1)
        TNT1 A 0 A_SpawnItemEx ("DetectFloorCraterSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 A 0 A_SpawnItemEx ("DetectCeilCraterSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 A 0 A_CustomMissile ("BluePlasmaFire", 0, 0, random (0, 360), 2, random (0, 360))
        TNT1 AAAAA 0 A_CustomMissile ("BluePlasmaParticle", 0, 0, random (0, 360), 2, random (0, 360))
        TNT1 B 4    
        TNT1 AAAAAAAAA 9 A_CustomMissile ("MiniSmoke", 1, 0, random (0, 360), 2, random (0, 160))
        Stop
    }
}

ACTOR BdRocket Replaces Rocket
{
    Radius 10
    Height 8
    Projectile
    Speed 35
    Damage 35
    DamageType Explosive
    MeleeDamage 0
    Gravity 0.00
    Scale 0.7
    Decal "Scorch"
    -BLOODSPLATTER
    -RANDOMIZE
    +EXTREMEDEATH
    +BLOODSPLATTER 
    +THRUSPECIES
    +MTHRUSPECIES
    SeeSound "weapons/rocket/fly"
    DeathSound "explosions/explode"
    Obituary "$OB_MPROCKET"
    Species "Marines"
    States
    {
    DMSpawn:
        TNT1 A 0
        TNT1 A 0 A_ChangeFLag("THRUSPECIES", 0)
        TNT1 A 0 A_ChangeFLag("MTHRUSPECIES", 0)
        Goto SpawnLoop    
    Spawn:
        TNT1 A 1
        TNT1 A 0 ACS_NamedExecute("adjustForDeathmatch", 0, 0, 0, 0)
    SpawnLoop:
        MISL A 1 Bright 
        TNT1 A 0 A_PlaySound("weapons/rocket/fly")
        TNT1 A 0 A_SpawnItem("RocketFlare",-20,0)
        TNT1 A 0 A_SpawnItem ("SmokeTrail")
        TNT1 A 0 A_CustomMissile ("SmokeTrail", 2, 0, random (160, 210), 2, random (-30, 30))
        Loop

    Death:
        TNT1 A 0 Radius_Quake (3, 8, 0, 15, 0)//(intensity, duration, damrad, tremrad, tid)
        TNT1 A 0 A_CustomMissile("BigRicochet")
        TNT1 A 0 A_SpawnItemEx ("DetectFloorCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 A 0 A_SpawnItemEx ("DetectCeilCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 A 0 A_SpawnItemEx ("ExplosionFlare",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 AAAAAAAAA 0 A_CustomMissile ("ExplosionParticleHeavy", 0, 0, random (0, 360), 2, random (0, 180))
        TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("ExplosionParticleHeavy", 0, 0, random (0, 360), 2, random (0, 360))
        TNT1 AAAAAAAAA 0 A_CustomMissile ("ExplosionParticleVeryFast", 0, 0, random (0, 360), 2, random (0, 360))
        TNT1 AAAAA 0 A_CustomMissile ("MediumExplosionFlames", 0, 0, random (0, 360), 2, random (0, 360))
        TNT1 AAAAA 0 A_CustomMissile ("ExplosionSmoke", 0, 0, random (0, 360), 2, random (0, 360))
        TNT1 A 0 A_SpawnItemEx ("RocketExplosionDamage",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 A 2
        TNT1 A 0 A_PlaySound("explosions/far", 3)
        TNT1 A 20
        Stop
    }
}

Actor SuperBFGBall : BFGBall replaces BFGBall
{
    +EXTREMEDEATH
    Damage 255
  Decal BFGLightning
    DeathSound "weapons/bfg/explode"
    Scale 0.7
    Species "Marines"
    +THRUSPECIES
    +MTHRUSPECIES
  States
  {
    DMSpawn:
        TNT1 A 0
        TNT1 A 0 A_ChangeFLag("THRUSPECIES", 0)
        TNT1 A 0 A_ChangeFLag("MTHRUSPECIES", 0)
        Goto Spawn1
    
    Spawn:
        TNT1 A 0
        TNT1 A 0 ACS_NamedExecute("adjustForDeathmatch", 0, 0, 0, 0)
    Spawn1:
    TNT1 A 0 A_SpawnItemEx("BFGFOG", 0, 0)
    BFS1 A 1 BRIGHT A_SpawnItem("GreenFlareMedium",0,0)
    BFS1 A 1 BRIGHT A_SpawnItemEx("BFGTrailParticle", Random(-13, 13), Random(-13, 13), Random(0, 18), Random(1, 3), 0, (0.1)*Random(-10, 10), Random(-20, 20), 128)
    TNT1 A 0 A_SpawnItemEx("BFGFOG", 0, 0)
    BFS1 B 1 BRIGHT A_SpawnItem("GreenFlareMedium",0,0)
    BFS1 B 1 BRIGHT A_SpawnItemEx("BFGTrailParticle", Random(-13, 13), Random(-13, 13), Random(0, 18), Random(1, 3), 0, (0.1)*Random(-10, 10), Random(-20, 20), 128)
    Loop
 
 Death:
    TNT1 A 0
    TNT1 A 0 Bright A_SpawnItem("GreenShockWave",0,0,0)
    TNT1 A 0 A_SpawnItemEx("BFGDeathParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
    TNT1 A 0 A_SpawnItemEx("BFGSuperParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
    TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("BFGBIGFOG", 0, 0, random (0, 360), 2, random (0, 360))
    TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("BFGBIGFOG", 0, 0, random (0, 360), 2, random (0, 360))
    TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("BFGSuperParticle", 0, 0, random (0, 360), 2, random (0, 360))
    BFGB AABBCCDDEE 1 Bright A_SpawnItem("GreenFlare",0,0)
    TNT1 A 0 A_Explode(80,300,0)
    BFGB F 1 Bright A_BFGSpray("SuperBFGExtra", 60, 20, 120)
    TNT1 A 0 A_SPawnItem("SuperBFGExtraGiant")
    BFGB GGHHIIJJKK 1 Bright A_SpawnItem("GreenFlare",0,0)
    Stop
  }
}


Actor BFGTrailParticle
{
  Height 0
  Radius 0
  Mass 0
  Speed 3
  +Missile
  +NoBlockMap
  +BLOODSPLATTER
  +DontSplash
  RenderStyle Add
  Scale 0.05
  States
  {
  Spawn:
    SPKG A 2 Bright A_FadeOut(0.02)
    Loop
  }
}



Actor BFGDeathParticle
{
  Height 0
  Radius 0
  Mass 0
  +Missile
  +NoBlockMap
  -BLOODSPLATTER
  +LowGravity
  +DontSplash
  +DoomBounce
  BounceFactor 0.5
  RenderStyle Add
  Scale 0.04
  States
  {
  Spawn:
  Death:
    SPKG A 2 Bright A_FadeOut(0.02)
    Loop
  }
}

Actor SuperBFGExtra : BFGExtra Replaces BFGExtra
{
  +NoBlockMap
  +BLOODSPLATTER
  RenderStyle Add
  +FORCERADIUSDMG
  +EXTREMEDEATH
  Alpha 0.75
  Damage 0
  Scale 0.6
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 AAAAA 0 A_SpawnItemEx("BFGExtraParticle", 0, 0, 14, (0.1)*Random(10, 40), 0, (0.1)*Random(-40, 40), Random(0, 360), 128)
    TNT1 AAAAA 0 A_SpawnItemEx("BFGExtraParticle", 0, 0, 14, (0.1)*Random(10, 40), 0, (0.1)*Random(-40, 40), Random(0, 360), 128)
    TNT1 AAAAA 0 A_SpawnItemEx("BFGExtraParticle", 0, 0, 14, (0.1)*Random(10, 40), 0, (0.1)*Random(-40, 40), Random(0, 360), 128)
    BFGB ABCDEFGHIJK 2 Bright A_SpawnItem("GreenFlareMedium",0,0)
    stop
  }
}


Actor SuperBFGExtraGiant : SuperBFGExtra
{
  Scale 2.9
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 AAAAA 0 A_SpawnItemEx("BFGExtraParticle", 0, 0, 14, (0.1)*Random(10, 40), 0, (0.1)*Random(-40, 40), Random(0, 360), 128)
    TNT1 AAAAA 0 A_SpawnItemEx("BFGExtraParticle", 0, 0, 14, (0.1)*Random(10, 40), 0, (0.1)*Random(-40, 40), Random(0, 360), 128)
    TNT1 AAAAA 0 A_SpawnItemEx("BFGExtraParticle", 0, 0, 14, (0.1)*Random(10, 40), 0, (0.1)*Random(-40, 40), Random(0, 360), 128)
    BFGB ABCDEFGHIJK 3 Bright A_SpawnItem("GreenFlareMedium",0,0)
    Stop
}}

Actor BFGExtraParticle
{
  Height 0
  Radius 0
  Mass 0
  +Missile
  +NoBlockMap
  +DontSplash
  +NoInteraction
  RenderStyle Add
  Scale 0.035
  States
  {
  Spawn:
  Death:
    SPKG A 2 Bright A_FadeOut(0.1)
    Loop
  }
}


ACTOR GreenShockWave 
{ 
   Speed 0
   Height 64 
   Radius 32
   Scale 2.25 
   RenderStyle add
   Alpha 0.9
   +DROPOFF 
   +NOBLOCKMAP 
   +BLOODSPLATTER 
   States 
   { 
   Spawn: 
      SHOK A 1 BRIGHT
      Goto Death 
   Death: 
      SHOK BCDEFFGGHHIIIJJJKKKLLMMNNOPQR 1 BRIGHT A_FadeOut(0.03)
      Stop 
   } 
} 

ACTOR BFGFOG
{
    Radius 1
    Height 1
    Alpha 0.7
    RenderStyle Add
    Scale 0.4
    Speed 8
    Gravity 0
    +NOBLOCKMAP
    +NOTELEPORT
    +DONTSPLASH
    +MISSILE
    +FORCEXYBILLBOARD
    +CLIENTSIDEONLY
    States
    {
    Spawn:
        BFOG ABCDEF 6 BRIGHT A_SpawnItem("GreenFlareMedium",0,0)
        Goto Death
    Death:
        BFOG G 3 BRIGHT A_SpawnItem("GreenFlareMedium",0,0)
        Stop
    }
}

ACTOR BFGBIGFOG: BFGFOG
{
    Scale 0.8
}

ACTOR SmallGreenFog: BFGFOG
{
    Speed 3
    Scale 0.5
}

Actor BFGDeathParticleSpawner
{
  +NoClip
  +NoBlockMap
  +BLOODSPLATTER
  +Missile
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 AAAAA 0 A_SpawnItemEx("BFGDeathParticle", 0, 0, 0, (0.1)*Random(20, 45), 0, (0.1)*Random(-40, 40), Random(0, 360), 128)
    TNT1 AAAAA 0 A_SpawnItemEx("BFGDeathParticle", 0, 0, 0, (0.1)*Random(20, 45), 0, (0.1)*Random(-40, 40), Random(0, 360), 128)
    TNT1 AAAAA 0 A_SpawnItemEx("BFGDeathParticle", 0, 0, 0, (0.1)*Random(20, 45), 0, (0.1)*Random(-40, 40), Random(0, 360), 128)
    TNT1 AAAAA 0 A_SpawnItemEx("BFGDeathParticle", 0, 0, 0, (0.1)*Random(20, 45), 0, (0.1)*Random(-40, 40), Random(0, 360), 128)
    TNT1 A 1
    Stop
  }
}

Actor BFGSuperParticleSpawner
{
  +NoClip
  +NoBlockMap
  +BLOODSPLATTER
  +Missile
  +FORCEXYBILLBOARD
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 AAAAAAAAAAA 0 A_SpawnItemEx("BFGSuperParticle", 0, 0, 0, (0.1)*Random(10, 35), 0, (0.1)*Random(-20, 20), Random(0, 360), 128)
    TNT1 AAAAAAAAAAA 0 A_SpawnItemEx("BFGSuperParticle", 0, 0, 0, (0.1)*Random(10, 35), 0, (0.1)*Random(-20, 20), Random(0, 360), 128)
    TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx("BFGSuperParticle", 0, 0, 0, (0.1)*Random(10, 35), 0, (0.1)*Random(-20, 20), Random(0, 360), 128)
    TNT1 A 1
    Stop
  }
}


Actor BFGSuperParticle
{
  Height 0
  Radius 0
  Mass 0
  +Missile
  +NoBlockMap
  +BLOODSPLATTER
  +DontSplash
  +FORCEXYBILLBOARD
  RenderStyle Add
  Scale 0.04
  Speed 24
  States
  {
  Spawn:
  Death:
    SPKG A 2 Bright A_FadeOut(0.02)
    Loop
  }
}

ACTOR FlakShard
  {
  Projectile
  +RANDOMIZE
  +FORCEXYBILLBOARD
  +BOUNCEONFLOORS
  +BOUNCEONCEILINGS
  +BLOODSPLATTER
  +NOTELEPORT
  BounceType Grenade
  bouncefactor 1
  BounceSound "impacts/ricochet"
  wallbouncefactor 1
  damage (10+5*random(0, 2))
  radius 2
  height 2
  speed 45
  alpha 0.9
  scale 0.4
  Decal "BulletChip"
  DamageType Flak
  states
    {
    Spawn:
        FKSD ABCDABCD 1 BRIGHT Light("FLAKHI")
        FKSD EFGHEFGHEFGH 1 BRIGHT Light("FLAKLO")
        TNT1 A 1 A_CustomMissile("FlakResidue",-5,0,0,CMF_AIMDIRECTION)
    Death:
        Stop
    XDeath:
        TNT1 A 0
        Stop
    }
}

ACTOR FlakResidue {
  +FORCEXYBILLBOARD
  +BOUNCEONFLOORS
  +BOUNCEONCEILINGS
  +THRUACTORS
  +NOTELEPORT
  BounceType Grenade
  bouncefactor 0.2
  wallbouncefactor 0.2
  BounceSound "weapons/shelldrop"
  radius 2
  height 2
  speed 45
  alpha 0.9
  scale 0.4
  states
    {
    Spawn:
        TNT1 AAAA 0 A_Jump(256, "Type1", "Type2", "Type3", "Type4")
    Type1:
        FKSD I 60
        Stop
    Type2:
        FKSD J 62
        Stop
    Type3:
        FKSD K 55
        Stop
    Type4:
        FKSD L 57
        Stop
    Death:
        Stop
    XDeath:
    TNT1 A 0
    Stop
    }
}

/** Inventory class used to count up the time until a grenade explodes */
Actor GrenadeTime : Inventory
{
inventory.maxamount 6
}

/** Grenades aren't really implemented as weapons, but they can be used with the Zoom function on weapons. They're projectiles that bounce and are affected by gravity. */
ACTOR BdGrenade
{
    Radius 4
    Height 4
    Projectile
    Speed 30
    Damage 0
    Gravity 0.7
    Scale 0.2
    +MISSILE
    -NOGRAVITY
    -BLOODSPLATTER
    -EXTREMEDEATH
    +EXPLODEONWATER
    +SKYEXPLODE
    +DOOMBOUNCE
    BounceFactor 0.5
    WallBounceFactor 0.25
    Health 5
    SeeSound "weapons/grenade/bounce"
    DeathSound "none"
    Obituary "%o ate %k grenade."
    States
    {
    Spawn:
        GRND ABCDEFGH 2
        TNT1 A 0 A_GiveInventory("GrenadeTime", 1)
        TNT1 A 0 A_JumpIfInventory("GrenadeTime", 3, "Explode")
        Loop
    Death:
        TNT1 A 0
        GRND H 16
        TNT1 A 0 A_GiveInventory("GrenadeTime", 1)
        TNT1 A 0 A_JumpIfInventory("GrenadeTime", 3, "Explode")
        Loop
        
        
        
    Explode:
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_SpawnItem("FragGrenadeExplosion")
        Stop
    }
}

actor FragGrenadeExplosion
{
+NOBLOCKMAP
+MISSILE
Damagetype Explosive
DeathSound "explosions/explode"
States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Explode(200,400, 0)
        TNT1 A 0 A_Explode(200,200, 0)
        TNT1 A 0 A_SpawnItem ("BigRicochet", 0, -15)
        TNT1 A 0 A_SpawnItemEx ("DetectFloorCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 A 0 A_SpawnItemEx ("DetectCeilCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 A 0 A_SpawnItemEx ("ExplosionFlare",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 A 0 A_SpawnItemEx ("BarrelExplosionDamage",0,0,30,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 A 0 A_SpawnItemEx ("BarrelKaboom",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 AAAAAAA 0 A_CustomMissile ("ExplosionFlames", 0, 0, random (0, 360), 2, random (0, 360))
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("ExplosionParticleHeavy", 0, 0, random (0, 360), 2, random (0, 180))
        TNT1 AAAAAA 0 A_CustomMissile ("ExplosionParticleHeavy", 0, 0, random (0, 360), 2, random (0, 180))
        TNT1 A 0 Radius_Quake (2, 24, 0, 15, 0)
        TNT1 A 0 BRIGHT A_Scream
        TNT1 A 0 A_ALertMonsters
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("ShrapnelParticle", 0, 0, random (0, 360), 2, random (5, 90))
        TNT1 A 0 A_PlaySound("explosions/far", 3)
        TNT1 A 0 A_SpawnItem("SmokeColumn")
        TNT1 AAAAAAAAAAAAAAA 8 A_CustomMissile ("ExplosionSmoke", 1, 0, random (0, 360), 2, random (50, 130))
        Stop
    }
}

/** Flag used to indicate the player is punching, used to animate the player */
Actor Punching : Inventory
{
inventory.maxamount 1
}

/** Flag given to the player to make them recoil when a kick has connected. */
Actor KickHasHit : Inventory
{
inventory.maxamount 1
}

/** Used to get the player to animate different melee attacks in a combo formation */
Actor PSeq1 : Inventory
{
inventory.maxamount 2
}

/** Used to get the player to animate different melee attacks in a combo formation */
Actor PSeq2 : Inventory
{
inventory.maxamount 1
}

ACTOR KickAttack: FastProjectile
{
    Radius 10
    Height 6
    DamageType Kick
    Projectile 
    +FORCEXYBILLBOARD
    +NOEXTREMEDEATH
    RenderStyle Add
    Alpha 0.6
    Damage (random(38,42))
    Speed 30
    SeeSound "none"
    DeathSound "none"
    States
    {
    Spawn:
        TNT1 A 1 BRIGHT
        TNT1 A 1
        Stop
    Death:
            TNT1 A 0 A_PlaySound("weapons/melee/heavy", 3)
            TNT1 A 0 A_GiveToTarget("KickHasHit", 1)
            TNT1 A 0 Radius_Quake (2, 6, 0, 5, 0)//(intensity, duration, damrad, tremrad, tid)
            TNT1 A 10
        Stop
    }
}

ACTOR SuperKickAttack: KickAttack
{
    Damage (random(64,70))
    -NOEXTREMEDEATH
    DamageType SuperKick
}

ACTOR MeleeStrike1: KickAttack
{
DamageType Kick
Damage (random(10,15))
Speed 30
States
{
    Spawn:
        TNT1 A 1 BRIGHT
        Stop
    Death:
        TNT1 A 0
        TNT1 A 0 A_PlaySound("weapons/melee/heavy")
        TNT1 A 10
        Stop
}
}


ACTOR MeleeStrike2: MeleeStrike1
{
Damage (random(24,32))
Speed 40
}

ACTOR MeleeStrike1Smash: MeleeStrike1
{
Damage (random(24,32))
DamageType SuperKick
}

ACTOR MeleeStrikeSuperHook: MeleeStrike1
{
Damage (random(72,80))
DamageType SuperKick
}
